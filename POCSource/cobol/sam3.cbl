000100****************************************************************
000200* LICENSED MATERIALS - PROPERTY OF IBM
000300* ALL RIGHTS RESERVED
000400****************************************************************
000500* PROGRAM:  SAM1
000600*
000700* AUTHOR :  Doug Stout
000800*
000900* READS A SEQUENTIAL TRANSACTION FILE AND MAKES UPDATES
001000* TO A SORTED SEQUENTIAL CUSTOMER FILE
001100*
001200* A GOOD CASE FOR DEBUGGING LAB - INDEED
001300*
001400* CAN BE MADE TO ABEND WITH BAD INPUT DATA FOR FAULT ANALYSIS LAB
001500*****************************************************************
001501
001600*
001700* Transaction file record descriptions:
001800*     0    1    1    2    2    3    3    4    4    5    5    6
001900* ....5....0....5....0....5....0....5....0....5....0....5....0....
002000*
002100* *  <== an asterisk in first column is a comment
002200* UPDATE ---key---- -command-- field-name ss -----------value-----
002300*                   can be:                  valid formats:
002400*                   REPLACE                  character_string_____
002500*                   ADD                      +99999999
002600*                   SUBTRACT                 +99999999.99
002700*       (The "ss" field is a subscript, used for the MONTH field o
002800* DELETE ___key____  <== Delete Record
002900* ADD    ___key____  <== Add a new blank record
003000*
003100*****************************************************************
003200 IDENTIFICATION DIVISION.
003300 PROGRAM-ID. SAM3.
003400 ENVIRONMENT DIVISION.
003500 INPUT-OUTPUT SECTION.
003600 FILE-CONTROL.
003700
003800     SELECT CUSTOMER-FILE ASSIGN TO CUSTFILE
003900         ACCESS IS SEQUENTIAL
004000         FILE STATUS  IS  WS-CUSTFILE-STATUS.
004100
004200     SELECT CUSTOMER-FILE-OUT ASSIGN TO CUSTOUT
004300         ACCESS IS SEQUENTIAL
004400         FILE STATUS  IS  WS-CUSTOUT-STATUS.
004500
004600      SELECT TRANSACTION-FILE ASSIGN TO TRANFILE
004700             FILE STATUS  IS  WS-TRANFILE-STATUS.
004800
004900      SELECT REPORT-FILE      ASSIGN TO CUSTRPT
005000             FILE STATUS  IS  WS-REPORT-STATUS.
005100
005200*****************************************************************
005300 DATA DIVISION.
005400 FILE SECTION.
005500
005600 FD  CUSTOMER-FILE
005602     RECORDING MODE IS F
005603     BLOCK CONTAINS 0 RECORDS.
005900 01  CUST-REC-FD                 PIC X(600).
006000
006100 FD  CUSTOMER-FILE-OUT
006200     RECORDING MODE IS V
006300     BLOCK CONTAINS 0 RECORDS
006400     RECORD IS VARYING FROM 20 TO 596 CHARACTERS.
006500 COPY CUSTCOPY REPLACING ==:TAG:== BY ==CSTOUT==.
006600
006700 FD  TRANSACTION-FILE
006800     RECORDING MODE IS F.
006900 COPY TRANREC.
007000
007100 FD  REPORT-FILE
007200     RECORDING MODE IS F.
007300 01  REPORT-RECORD              PIC X(132).
007400
007500*****************************************************************
007600 WORKING-STORAGE SECTION.
007700*****************************************************************
007800*
007900 01  SYSTEM-DATE-AND-TIME.
008000     05  CURRENT-DATE.
008100         10  CURRENT-YEAR            PIC 9(2).
008200         10  CURRENT-MONTH           PIC 9(2).
008300         10  CURRENT-DAY             PIC 9(2).
008400     05  CURRENT-TIME.
008500         10  CURRENT-HOUR            PIC 9(2).
008600         10  CURRENT-MINUTE          PIC 9(2).
008700         10  CURRENT-SECOND          PIC 9(2).
008800         10  CURRENT-HNDSEC          PIC 9(2).
008900*
009000 01  CUST-REC.
009100     05  CUST-KEY.
009200         10  CUST-ID             PIC X(5).
009300         10  CUST-REC-TYPE       PIC X.
009400     05  CUST-NAME               PIC X(17).
009500     05  CUST-ACCT-BALANCE       PIC S9(7)V99  COMP-3.
009600     05  CUST-ORDERS-YTD         PIC S9(5)     COMP.
009700     05  CUST-ADDR               PIC X(20).
009800     05  CUST-CITY               PIC X(14).
009900     05  CUST-STATE              PIC X(02).
010000     05  CUST-COUNTRY            PIC X(11).
010100     05  CUST-MONTH              PIC S9(7)V99 COMP-3 OCCURS 12.
010200     05  CUST-OCCUPATION         PIC X(30).
010300     05  CUST-NOTES              PIC X(120).
010400     05  CUST-DATA-1             PIC X(05).
010500     05  CUST-DATA-2             PIC X(40).
010600*
010700 01  WS-FIELDS.
010800     05  WS-CUSTFILE-STATUS      PIC X(2)  VALUE SPACES.
010900     05  WS-CUSTOUT-STATUS       PIC X(2)  VALUE SPACES.
011000     05  WS-TRANFILE-STATUS      PIC X(2)  VALUE SPACES.
011100     05  WS-REPORT-STATUS        PIC X(2)  VALUE SPACES.
011200     05  WS-TRAN-EOF             PIC X     VALUE SPACES.
011300     05  WS-TRAN-OK              PIC X     VALUE 'N'.
011400     05  WS-CUST-FILE-OK         PIC X     VALUE 'N'.
011500     05  WS-CUST-FILE-EOF        PIC X     VALUE 'N'.
011600     05  WS-TRAN-MSG             PIC X(50) VALUE SPACES.
011700     05  WS-PREV-TRAN-KEY        PIC X(10) VALUE LOW-VALUES.
011800     05  INCR-CUST-ID            PIC 9(5)  VALUE 0.
011900     05  START-CUST-ID           PIC 9(5)  VALUE 0.
012000     05  MAX-CUST-ID             PIC 9(5)  VALUE 0.
012001     05  TEST-BASELINE           PIC 9(4)  COMP-3  VALUE 0.
012100*
012200 01  WORK-VARIABLES.
012300     05  I                     PIC S9(9)   COMP-3  VALUE +0.
012400     05  WORK-NUM              PIC S9(8)   COMP-3  VALUE +0.
012500*
012600 01  REPORT-TOTALS.
012700     05  NUM-TRAN-RECS         PIC S9(9)   COMP-3  VALUE +0.
012800     05  NUM-TRAN-ERRORS       PIC S9(9)   COMP-3  VALUE +0.
012900     05  NUM-ADD-REQUESTS      PIC S9(9)   COMP-3  VALUE +0.
013000     05  NUM-ADD-PROCESSED     PIC S9(9)   COMP-3  VALUE +0.
013100     05  NUM-UPDATE-REQUESTS   PIC S9(9)   COMP-3  VALUE +0.
013200     05  NUM-UPDATE-PROCESSED  PIC S9(9)   COMP-3  VALUE +0.
013300     05  NUM-DELETE-REQUESTS   PIC S9(9)   COMP-3  VALUE +0.
013400     05  NUM-DELETE-PROCESSED  PIC S9(9)   COMP-3  VALUE +0.
013500     05  NUM-CRUNCH-REQUESTS   PIC S9(9)   COMP-3  VALUE +0.
013600     05  NUM-CRUNCH-PROCESSED  PIC S9(9)   COMP-3  VALUE +0.
013700     05  NUM-RPTALL-REQUESTS   PIC S9(9)   COMP-3  VALUE +0.
013800     05  NUM-RPTALL-PROCESSED  PIC S9(9)   COMP-3  VALUE +0.
013900     05  NUM-GEN-REQUESTS      PIC S9(9)   COMP-3  VALUE +0.
014000     05  NUM-GEN-PROCESSED     PIC S9(9)   COMP-3  VALUE +0.
014100
014200 COPY CUSTCOPY REPLACING ==:TAG:== BY ==WS-CUST==.
014300
014400*        *******************
014500*            report lines
014600*        *******************
014700 01  ERR-MSG-BAD-TRAN.
014800     05  FILLER PIC X(31)
014900                  VALUE 'Error Processing Transaction. '.
015000     05  ERR-MSG-DATA1              PIC X(35)  VALUE SPACES.
015100     05  ERR-MSG-DATA2              PIC X(66)  VALUE SPACES.
015200 01  ERR-MSG-BAD-TRAN-2.
015300     05  FILLER                     PIC X(21)  VALUE SPACES.
015400     05  ERR-MSG-DATA3              PIC X(80).
015500     05  FILLER                     PIC X(31)  VALUE SPACES.
015600 01  MSG-TRAN-SCALE-1.
015700     05  FILLER PIC X(21) VALUE SPACES.
015800     05  FILLER                     PIC X(35)
015900                    VALUE '         1    1    2    2    3    3'.
016000     05  FILLER                     PIC X(35)
016100                    VALUE '    4    4    5    5    6    6    7'.
016200     05  FILLER                     PIC X(41)  VALUE SPACES.
016300 01  MSG-TRAN-SCALE-2.
016400     05  FILLER PIC X(21) VALUE ' Transaction Record: '.
016500     05  FILLER                     PIC X(35)
016600                    VALUE '....5....0....5....0....5....0....5'.
016700     05  FILLER                     PIC X(35)
016800                    VALUE '....0....5....0....5....0....5....0'.
016900     05  FILLER                     PIC X(41)  VALUE SPACES.
017000 01 RPT-HEADER1.
017100     05  FILLER                     PIC X(40)
017200               VALUE 'CUSTOMER FILE UPDATE REPORT       DATE: '.
017300     05  RPT-MM                     PIC 99.
017400     05  FILLER                     PIC X     VALUE '/'.
017500     05  RPT-DD                     PIC 99.
017600     05  FILLER                     PIC X     VALUE '/'.
017700     05  RPT-YY                     PIC 99.
017800     05  FILLER                     PIC X(20)
017900                    VALUE ' (mm/dd/yy)   TIME: '.
018000     05  RPT-HH                     PIC 99.
018100     05  FILLER                     PIC X     VALUE ':'.
018200     05  RPT-MIN                    PIC 99.
018300     05  FILLER                     PIC X     VALUE ':'.
018400     05  RPT-SS                     PIC 99.
018500     05  FILLER                     PIC X(55) VALUE SPACES.
018600 01  RPT-TRAN-DETAIL1.
018700     05  RPT-TRAN-MSG1      PIC X(31)
018800                  VALUE '       Transaction processed: '.
018900     05  RPT-TRAN-RECORD            PIC X(80)  VALUE SPACES.
019000     05  FILLER                     PIC X(21)  VALUE SPACES.
019100 01  RPT-STATS-HDR1.
019200     05  FILLER PIC X(26) VALUE 'Transaction Totals:       '.
019300     05  FILLER PIC X(107) VALUE SPACES.
019400 01  RPT-STATS-HDR2.
019500     05  FILLER PIC X(26) VALUE 'Transaction      Number of'.
019600     05  FILLER PIC X(28) VALUE '        Number        Number'.
019700     05  FILLER PIC X(79) VALUE SPACES.
019800 01  RPT-STATS-HDR3.
019900     05  FILLER PIC X(26) VALUE 'Type          Transactions'.
020000     05  FILLER PIC X(28) VALUE '     Processed      In Error'.
020100     05  FILLER PIC X(79) VALUE SPACES.
020200 01  RPT-STATS-HDR4.
020300     05  FILLER PIC X(26) VALUE '-----------   ------------'.
020400     05  FILLER PIC X(28) VALUE '   -----------   -----------'.
020500     05  FILLER PIC X(79) VALUE SPACES.
020600 01  RPT-STATS-DETAIL.
020700     05  RPT-TRAN            PIC X(10).
020800     05  FILLER              PIC X(4)     VALUE SPACES.
020900     05  RPT-NUM-TRANS       PIC ZZZ,ZZZ,ZZ9.
021000     05  FILLER              PIC X(3)     VALUE SPACES.
021100     05  RPT-NUM-TRAN-PROC   PIC ZZZ,ZZZ,ZZ9.
021200     05  FILLER              PIC X(3)     VALUE SPACES.
021300     05  RPT-NUM-TRAN-ERR    PIC ZZZ,ZZZ,ZZ9.
021400     05  FILLER              PIC X(80)   VALUE SPACES.
021500
021600 01  CUST-KEY-DIAG            PIC X(6).
021601 01  REF-MOD-VAL              PIC X(4).
021602 01  REF-MOD-1                PIC S9(1) VALUE 1.
021603 01  REF-MOD-2                PIC S9(1) VALUE 4.
021604 01  ABEND-TEST              PIC X(2).
021700 01  ABEND-TEST-N REDEFINES ABEND-TEST PIC S9(3) COMP-3.
021800
021900*****************************************************************
022000 PROCEDURE DIVISION.
022100*****************************************************************
022200
022300 000-MAIN.
022400     ACCEPT CURRENT-DATE FROM DATE.
022500     ACCEPT CURRENT-TIME FROM TIME.
022600     DISPLAY 'SAM1 STARTED DATE =   ' CURRENT-MONTH '/'
022700            CURRENT-DAY '/'  CURRENT-YEAR '   (mm/dd/yy)'.        JSS
022701*           UPON CONSOLE.
022800     DISPLAY '             TIME = ' CURRENT-HOUR ':'
022900            CURRENT-MINUTE ':' CURRENT-SECOND .
022901*           UPON CONSOLE.
022902
023000
023100     PERFORM 700-OPEN-FILES .
023200     PERFORM 800-INIT-REPORT .
023300
023400     PERFORM 730-READ-CUSTOMER-FILE .
023500     PERFORM 100-PROCESS-TRANSACTIONS
023600             UNTIL WS-TRAN-EOF =  'Y' .
023700
023800     PERFORM 850-REPORT-TRAN-STATS .
023900     PERFORM 790-CLOSE-FILES .
024000
024100     GOBACK .
024200
024300 100-PROCESS-TRANSACTIONS.
024400     PERFORM 710-READ-TRAN-FILE.
024500
024600     IF WS-TRAN-EOF NOT = 'Y'
024700         COMPUTE NUM-TRAN-RECS =  NUM-TRAN-RECS + 1
024800         MOVE 'Y' TO WS-TRAN-OK
024801         MOVE TRAN-KEY(REF-MOD-1:REF-MOD-2) TO REF-MOD-VAL
024900         IF TRAN-KEY < WS-PREV-TRAN-KEY
025000            MOVE 'TRANSACTION OUT OF SEQUENCE' TO ERR-MSG-DATA1
025100            MOVE SPACES TO ERR-MSG-DATA2
025200            PERFORM 299-REPORT-BAD-TRAN
025300         ELSE
025400           EVALUATE TRAN-CODE
025601              WHEN  'UPDATE'
025602                  PERFORM 200-PROCESS-UPDATE-TRAN
025700              WHEN 'ADD   '
025800                  PERFORM 210-PROCESS-ADD-TRAN
025900              WHEN 'DELETE'
026000                  PERFORM 220-PROCESS-DELETE-TRAN
026001              WHEN 'CRUNCH'
026002                  PERFORM 200-PROCESS-CRUNCH-TRAN
026100              WHEN OTHER
026200                  IF TRAN-COMMENT NOT = '*'
026300                    MOVE 'INVALID TRANSACTION:' TO ERR-MSG-DATA1
026400                    MOVE TRAN-CODE TO ERR-MSG-DATA2
026500                    PERFORM 299-REPORT-BAD-TRAN
026600                  END-IF
026700           END-EVALUATE
026800         END-IF
026900         MOVE TRAN-KEY TO WS-PREV-TRAN-KEY
027000         IF WS-TRAN-OK  = 'Y'
027100             PERFORM 830-REPORT-TRAN-PROCESSED
027200         END-IF
027300     END-IF .
027400
027500
027600 200-PROCESS-UPDATE-TRAN.
027700     ADD +1 TO NUM-UPDATE-REQUESTS.
027701     MOVE CUST-KEY  TO CUST-KEY-DIAG.
027800     PERFORM 720-POSITION-CUST-FILE.
027900     IF CUST-KEY NOT = TRAN-KEY OR WS-CUST-FILE-EOF =   'Y'       JSS
028000         MOVE 'NO MATCHING KEY FOUND' TO ERR-MSG-DATA1
028100         MOVE TRAN-KEY  TO ERR-MSG-DATA2
028200         PERFORM 299-REPORT-BAD-TRAN
028300     ELSE
028400*
028500*        Subroutine SAM4 will apply an update to a customer record
028600*
028700         CALL 'SAM4' USING CUST-REC,  TRANSACTION-RECORD,         JSS
028800                                WS-TRAN-OK, WS-TRAN-MSG
028900         IF WS-TRAN-OK NOT = 'Y'
029000             MOVE WS-TRAN-MSG TO ERR-MSG-DATA1
029100             MOVE SPACES      TO ERR-MSG-DATA2
029200             PERFORM 299-REPORT-BAD-TRAN
029300         ELSE
029400             ADD +1 TO NUM-UPDATE-PROCESSED
029500         END-IF
029501*     CALL 'DBGMAIN'
029600     END-IF .
029700
029701 200-PROCESS-CRUNCH-TRAN.
029702     ADD +1 TO NUM-CRUNCH-REQUESTS.
029710*
029711*        Subroutine SAM4 will apply an update to a customer record
029712*
029713         CALL 'SAM4' USING CUST-REC, TRANSACTION-RECORD,
029714                                WS-TRAN-OK, WS-TRAN-MSG
029715         IF WS-TRAN-OK NOT = 'Y'
029716             MOVE WS-TRAN-MSG TO ERR-MSG-DATA1
029717             MOVE SPACES      TO ERR-MSG-DATA2
029718             PERFORM 299-REPORT-BAD-TRAN
029719         ELSE
029720             ADD +1 TO NUM-CRUNCH-PROCESSED
029723     END-IF .
029724
029800 210-PROCESS-ADD-TRAN.
029900     ADD +1 TO NUM-ADD-REQUESTS .
030000     PERFORM 720-POSITION-CUST-FILE.
030100     IF CUST-KEY = TRAN-KEY
030200         MOVE 'DUPLICATE KEY:       ' TO ERR-MSG-DATA1
030300         MOVE TRAN-KEY  TO ERR-MSG-DATA2
030400         PERFORM 299-REPORT-BAD-TRAN
030500     ELSE
030600         MOVE SPACES TO WS-CUST-REC
030700         MOVE TRAN-KEY TO WS-CUST-KEY
030800         MOVE +0 TO WS-CUST-ACCT-BALANCE
030900         MOVE +0 TO WS-CUST-ORDERS-YTD
031000         PERFORM TEST AFTER VARYING I FROM 1 BY 1
031100           UNTIL I > 12
031200             MOVE +0 TO WS-CUST-MONTH(I)
031300         END-PERFORM
031400         PERFORM 740-WRITE-CUSTOUT-FILE
031500         ADD +1 TO NUM-ADD-PROCESSED
031600     END-IF .
031700
031800 220-PROCESS-DELETE-TRAN.
031900     ADD +1 TO NUM-DELETE-REQUESTS.
032000     PERFORM 720-POSITION-CUST-FILE.
032100     IF CUST-KEY NOT = TRAN-KEY OR WS-CUST-FILE-EOF = 'Y'
032200         MOVE 'NO MATCHING KEY:     ' TO ERR-MSG-DATA1
032300         MOVE TRAN-KEY  TO ERR-MSG-DATA2
032400         PERFORM 299-REPORT-BAD-TRAN
032500     ELSE
032600         ADD +1 TO NUM-DELETE-PROCESSED
032700         PERFORM 730-READ-CUSTOMER-FILE
032800     END-IF .
032900
033000 299-REPORT-BAD-TRAN.
033100     ADD +1 TO NUM-TRAN-ERRORS.
033200     MOVE 'N' TO WS-TRAN-OK.
033300     WRITE REPORT-RECORD FROM ERR-MSG-BAD-TRAN  AFTER 2.
033400     WRITE REPORT-RECORD FROM MSG-TRAN-SCALE-1.
033500     WRITE REPORT-RECORD FROM MSG-TRAN-SCALE-2.
033600     MOVE TRANSACTION-RECORD   TO ERR-MSG-DATA3.
033700     WRITE REPORT-RECORD FROM ERR-MSG-BAD-TRAN-2.
033800
033900 700-OPEN-FILES.
034000     OPEN INPUT    TRANSACTION-FILE
034100                   CUSTOMER-FILE
034200          OUTPUT   CUSTOMER-FILE-OUT
034300                   REPORT-FILE .
034400     IF WS-CUSTFILE-STATUS NOT = '00'
034500       DISPLAY 'ERROR OPENING CUSTOMER INPUT FILE. RC:'
034600               WS-CUSTFILE-STATUS
034700       DISPLAY 'Terminating Program due to File Error'
034800       MOVE 16 TO RETURN-CODE
034900       MOVE 'Y' TO WS-TRAN-EOF
035000     END-IF .
035100     IF WS-CUSTOUT-STATUS NOT = '00'
035200       DISPLAY 'ERROR OPENING CUSTOMER OUTPUT FILE. RC:'
035300               WS-CUSTOUT-STATUS
035400       DISPLAY 'Terminating Program due to File Error'
035500       MOVE 16 TO RETURN-CODE
035600       MOVE 'Y' TO WS-TRAN-EOF
035700     END-IF .
035800     IF WS-TRANFILE-STATUS NOT = '00'
035900       DISPLAY 'ERROR OPENING TRAN FILE. RC:' WS-TRANFILE-STATUS
036000       DISPLAY 'Terminating Program due to File Error'
036100       MOVE 16 TO RETURN-CODE
036200       MOVE 'Y' TO WS-TRAN-EOF
036300     END-IF .
036400
036500
036600 710-READ-TRAN-FILE.
036700     READ TRANSACTION-FILE
036800       AT END MOVE 'Y' TO WS-TRAN-EOF .
036900*    MOVE SPACES TO ABEND-TEST
037000*    ADD 1 TO ABEND-TEST-N
037100     EVALUATE      WS-TRANFILE-STATUS
037200        WHEN '00'
037300             CONTINUE
037400        WHEN '10'
037500             MOVE 'Y' TO WS-TRAN-EOF
037600        WHEN OTHER
037700            MOVE 'Error on tran file read.  Code:'
037800                        TO ERR-MSG-DATA1
037900            MOVE WS-CUSTFILE-STATUS TO ERR-MSG-DATA2
038000            PERFORM 299-REPORT-BAD-TRAN
038100            MOVE 'Y' TO WS-TRAN-EOF
038200     END-EVALUATE .
038300     IF WS-TRAN-EOF = 'Y'
038400         PERFORM 721-COPY-RECORDS
038500           UNTIL WS-CUST-FILE-EOF = 'Y'
038600     END-IF .
038700
038800 720-POSITION-CUST-FILE.
038900     IF CUST-KEY < TRAN-KEY
039000         IF WS-CUST-FILE-EOF NOT = 'Y'
039100             PERFORM 721-COPY-RECORDS
039200               UNTIL CUST-KEY >= TRAN-KEY
039300                  OR WS-CUST-FILE-EOF = 'Y'
039400         END-IF
039500     END-IF .
039600
039700 721-COPY-RECORDS.
039800     MOVE CUST-REC TO WS-CUST-REC .
039900     PERFORM 740-WRITE-CUSTOUT-FILE .
040000     PERFORM 730-READ-CUSTOMER-FILE .
040100
040200 730-READ-CUSTOMER-FILE.
040300     READ CUSTOMER-FILE INTO CUST-REC
040400       AT END MOVE 'Y' TO WS-CUST-FILE-EOF .
040500     EVALUATE WS-CUSTFILE-STATUS
040600        WHEN '00'
040700        WHEN '04'
040800            CONTINUE
040900        WHEN '10'
041000            MOVE 'Y' TO WS-CUST-FILE-EOF
041100        WHEN OTHER
041200            MOVE 'Customer input File I/O Error on Read.  RC: '
041300                        TO ERR-MSG-DATA1
041400            MOVE WS-CUSTFILE-STATUS TO ERR-MSG-DATA2
041500            PERFORM 299-REPORT-BAD-TRAN
041600     END-EVALUATE .
041700
041800 740-WRITE-CUSTOUT-FILE.
041900     IF WS-CUST-REC-TYPE = 'A'
042000         WRITE CSTOUT-REC FROM WS-CUST-REC
042100     ELSE
042200         MOVE WS-CUST-REC  TO  WS-CUST-CONTACT-REC
042300         WRITE CSTOUT-CONTACT-REC FROM WS-CUST-CONTACT-REC
042400     END-IF .
042500     EVALUATE WS-CUSTOUT-STATUS
042600        WHEN '00'
042700            CONTINUE
042800        WHEN OTHER
042900            MOVE 'CUSTOMER OUTPUT FILE I/O ERROR ON WRITE. RC: '
043000                        TO ERR-MSG-DATA1
043100            MOVE WS-CUSTFILE-STATUS TO ERR-MSG-DATA2
043200            PERFORM 299-REPORT-BAD-TRAN
043300     END-EVALUATE .
043400
043500 790-CLOSE-FILES.
043600     CLOSE TRANSACTION-FILE .
043700     CLOSE REPORT-FILE .
043800     CLOSE CUSTOMER-FILE .
043900
044000 800-INIT-REPORT.
044100     MOVE CURRENT-YEAR   TO RPT-YY.
044200     MOVE CURRENT-MONTH  TO RPT-MM.
044300     MOVE CURRENT-DAY    TO RPT-DD.
044400     MOVE CURRENT-HOUR   TO RPT-HH.
044500     MOVE CURRENT-MINUTE TO RPT-MIN.
044600     MOVE CURRENT-SECOND TO RPT-SS.
044700     WRITE REPORT-RECORD FROM RPT-HEADER1 AFTER PAGE.
044800
044900 830-REPORT-TRAN-PROCESSED.
045000     MOVE TRANSACTION-RECORD TO RPT-TRAN-RECORD.
045100     IF TRAN-COMMENT = '*'
045200         MOVE SPACES TO RPT-TRAN-MSG1
045300     ELSE
045400         MOVE '       Transaction processed: ' TO RPT-TRAN-MSG1
045500     END-IF.
045600     WRITE REPORT-RECORD FROM RPT-TRAN-DETAIL1.
045700
045800 850-REPORT-TRAN-STATS.
045900     WRITE REPORT-RECORD FROM RPT-STATS-HDR1 AFTER 2.
046000     WRITE REPORT-RECORD FROM RPT-STATS-HDR2 AFTER 2.
046100     WRITE REPORT-RECORD FROM RPT-STATS-HDR3 AFTER 1.
046200     WRITE REPORT-RECORD FROM RPT-STATS-HDR4 AFTER 1.
046300
046400     MOVE 'ADD    '            TO RPT-TRAN.
046500     MOVE NUM-ADD-REQUESTS      TO RPT-NUM-TRANS.
046600     MOVE NUM-ADD-PROCESSED     TO RPT-NUM-TRAN-PROC.
046700     COMPUTE RPT-NUM-TRAN-ERR =
046800                NUM-ADD-REQUESTS  -  NUM-ADD-PROCESSED .
046900     WRITE REPORT-RECORD  FROM  RPT-STATS-DETAIL.
047000
047100     MOVE 'DELETE '            TO RPT-TRAN.
047200     MOVE NUM-DELETE-REQUESTS   TO RPT-NUM-TRANS.
047300     MOVE NUM-DELETE-PROCESSED  TO RPT-NUM-TRAN-PROC.
047400     COMPUTE RPT-NUM-TRAN-ERR =
047500                NUM-DELETE-REQUESTS  -  NUM-DELETE-PROCESSED .
047600     WRITE REPORT-RECORD  FROM  RPT-STATS-DETAIL.
047700
047800     MOVE 'UPDATE '            TO RPT-TRAN.
047900     MOVE NUM-UPDATE-REQUESTS   TO RPT-NUM-TRANS.
048000     MOVE NUM-UPDATE-PROCESSED  TO RPT-NUM-TRAN-PROC.
048100     COMPUTE RPT-NUM-TRAN-ERR =
048200                NUM-UPDATE-REQUESTS  -  NUM-UPDATE-PROCESSED .
048300     WRITE REPORT-RECORD  FROM  RPT-STATS-DETAIL.
