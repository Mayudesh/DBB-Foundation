000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    CNTRLBRK.
000300 AUTHOR.        IBM
000400 DATE-WRITTEN.  02-26-17.
000500
000600******************************************************************
000700*  PROGRAM USED TO CREATE CONTROL BREAK REPORT: CNTRLBRK SELECTS *
000800*  NON-PAID INVOICES FROM THE INVOICE FILE, PERFORMS INPUT DATA  *
000900*  VALIDATION, SORTS THE RECORDS IN CUSTOMER NAME AND INVOICE    *
001000*  NUMBER ORDER, THEN CREATES A NEW SORTED OUTPUT FILE OF NON-   *
001100*  PAID INVOICES AND A PRINTED OUTPUT REPORT.  ALL ERROR CON-    *
001200*  CONDITIONS ARE DISPLAYED TO SYSOUT.                           *
001300******************************************************************
001400
001500 ENVIRONMENT DIVISION.
001600
001700 CONFIGURATION SECTION.
001800 SOURCE-COMPUTER.   IBM-3090.
001900 OBJECT-COMPUTER.   IBM-3090.
002000
002100 INPUT-OUTPUT SECTION.
002200 FILE-CONTROL.
002300
002400     SELECT MI-INPUT-FILE ASSIGN TO UT-S-ACT2DATA.
002500     SELECT SW-MI-SORT-FILE ASSIGN TO UT-S-SORTFILE.
002600     SELECT MI-OUTPUT-SRT-FILE ASSIGN TO UT-S-SORT2FIL.
002700     SELECT MATB-OUTPUT-RPT ASSIGN TO UT-S-REPORT.
002800
002900
003000 DATA DIVISION.
003100
003200 FILE SECTION.
003300
003400 FD  MI-INPUT-FILE
003500     RECORDING MODE IS F
003600     LABEL RECORDS ARE STANDARD
003700     RECORD CONTAINS 80 CHARACTERS
003800     BLOCK CONTAINS 0 RECORDS
003900     DATA RECORD IS MI-INPUT-RECORD.
004000
004100 01  MI-INPUT-RECORD                  PIC X(80).
004200
004300 FD  MI-OUTPUT-SRT-FILE
004400     RECORDING MODE IS F
004500     LABEL RECORDS ARE STANDARD
004600     RECORD CONTAINS 80 CHARACTERS
004700     BLOCK CONTAINS 0 RECORDS
004800     DATA RECORD IS MI-OUTPUT-SRT-RECORD.
004900
005000 01  MI-OUTPUT-SRT-RECORD             PIC X(80).
005100
005200 FD  MATB-OUTPUT-RPT
005300     RECORDING MODE IS F
005400     LABEL RECORDS ARE STANDARD
005500     RECORD CONTAINS 133 CHARACTERS
005600     BLOCK CONTAINS 0 RECORDS
005700     DATA RECORD IS MATB-OUTPUT-REP-LINE.
005800
005900 01  MATB-OUTPUT-REP-LINE             PIC X(133).
006000
006100
006200 SD  SW-MI-SORT-FILE
006300     RECORD CONTAINS 80 CHARACTERS
006400     DATA RECORD IS SW-MI-SORT-WORK.
006500
006600 01  SW-MI-SORT-WORK.
006700     05  CUST-NO-SRT-WK               PIC 99.
006800     05  CUST-NAME-SRT-WK             PIC X(15).
006900     05  INV-NO-SRT-WK                PIC X(5).
007000     05  AGED-CODE-SRT-WK             PIC 9.
007100     05  INV-AMT-SRT-WK               PIC S9(7)V99.
007200     05  PAID-DATE-SRT-WK             PIC X(6).
007300     05  FILLER                       PIC X(42).
007400
007500 WORKING-STORAGE SECTION.
007600
007700 01  PROGRAM-INDICATOR-SWITCHES.
007800     05  WS-EOF-INPUT-SW              PIC X(3)       VALUE 'NO '.
007900         88  EOF-INPUT                               VALUE 'YES'.
008000     05  WS-EOF-SRT-OUTPUT-SW         PIC X(3)       VALUE 'NO '.
008100         88  EOF-SRT-OUTPUT                          VALUE 'YES'.
008200     05  WS-INPUT-OK-SW               PIC X(3)       VALUE 'NO '.
008300         88  INPUT-OK                                VALUE 'YES'.
008400     05  WS-NAME-FOUND-SW             PIC X(3)       VALUE SPACES.
008500         88  NAME-FOUND                              VALUE 'YES'.
008600         88  NAME-NOT-FOUND                          VALUE 'NO '.
008700     05  WS-PAID-SW                   PIC X(3)       VALUE SPACES.
008800         88  UNPAID                                  VALUE 'NO '.
008900         88  PAID                                    VALUE 'YES'.
009000
009100 01  WS-REPORT-CONTROLS.
009200     05  WS-PAGE-COUNT                PIC S9(3)     VALUE ZERO.
009300     05  WS-LINES-PER-PAGE            PIC S9(2)     VALUE +50.
009400     05  WS-LINES-USED                PIC S9(2)     VALUE +51.
009500     05  WS-LINE-SPACING              PIC S9(1)     VALUE ZERO.
009600
009700 01  WS-BREAK-CONTROLS.
009800     05  WS-PREVIOUS-CUST-NO          PIC 9(2).
009900
010000     copy cobtable.
010100
010200 01  WS-ACCUMULATORS.
010300*  FOR PROGRAM RECORD TRACKING
010400     05  WS-READ-CTR       PIC 9(4)       VALUE ZERO.
010500     05  WS-REL-CTR        PIC 9(4)       VALUE ZERO.
010600     05  WS-RETR-CTR       PIC 9(4)       VALUE ZERO.
010700     05  WS-WRTN-CTR       PIC 9(4)       VALUE ZERO.
010800     05  WS-PAID-CTR       PIC 9(4)       VALUE ZERO.
010900     05  WS-UNPD-CTR       PIC 9(4)       VALUE ZERO.
011000
011100
011200*  FOR CUSTOMER TOTALS
011300     05  WS-CURRENT-TL     PIC 9(7)V99    VALUE ZERO.
011400     05  WS-OVER-30-TL     PIC 9(7)V99    VALUE ZERO.
011500     05  WS-OVER-60-TL     PIC 9(7)V99    VALUE ZERO.
011600     05  WS-OVER-90-TL     PIC 9(7)V99    VALUE ZERO.
011700     05  WS-CUST-TOTAL-TL  PIC 9(7)V99    VALUE ZERO.
011800
011900*  FOR REPORT GRAND TOTALS
012000     05  WS-CURRENT-GTL    PIC 9(7)V99    VALUE ZERO.
012100     05  WS-OVER-30-GTL    PIC 9(7)V99    VALUE ZERO.
012200     05  WS-OVER-60-GTL    PIC 9(7)V99    VALUE ZERO.
012300     05  WS-OVER-90-GTL    PIC 9(7)V99    VALUE ZERO.
012400
012500
012600* PROGRAM FILES DEFINED:
012700
012800
012900 01  MI-INPUT-REC.
013000     05  CUST-NO-IN                   PIC 99.
013100     05  INV-NO-IN                    PIC X(5).
013200     05  INV-NO-IN-NUM   REDEFINES   INV-NO-IN         PIC 9(5).
013300     05  AGED-CODE-IN                 PIC 9.
013400     05  INV-AMT-IN                   PIC S9(7)V99.
013500     05  PAID-DATE-IN                 PIC X(6).
013600     05  PAID-DATE-IN-NUM   REDEFINES   PAID-DATE-IN   PIC 9(6).
013700     05  FILLER                       PIC X(57).
013800
013900 01  MI-OUTPUT-SRT-REC.
014000     05  CUST-NO-SRT                  PIC 99.
014100     05  CUST-NAME-SRT                PIC X(15).
014200     05  INV-NO-SRT                   PIC X(5).
014300     05  AGED-CODE-SRT                PIC 9.
014400     05  INV-AMT-SRT                  PIC S9(7)V99.
014500     05  PAID-DATE-SRT                PIC X(6).
014600     05  FILLER                       PIC X(42).
014700
014800
014900* PROGRAM REPORT LINES.
015000
015100 01  HL-HEADER-1.
015200     05  FILLER            PIC X(1)   VALUE SPACES.
015300     05  FILLER            PIC X(15)  VALUE 'REPORT NO 12345'.
015400     05  FILLER            PIC X(34)  VALUE SPACES.
015500     05  FILLER            PIC X(25)
015600                  VALUE 'AGED TRIAL BALANCE AS OF '.
015700     05  RPT-DATE          PIC X(8).
015800     05  FILLER            PIC X(29)  VALUE SPACES.
015900     05  FILLER            PIC X(5)   VALUE 'PAGE '.
016000     05  RPT-PAGE-NO       PIC ZZZ.
016100     05  FILLER            PIC X(12)  VALUE SPACES.
016200
016300 01  HL-HEADER-2.
016400     05  FILLER            PIC X(3)   VALUE SPACES.
016500     05  FILLER            PIC X(10)  VALUE 'CUST NO   '.
016600     05  FILLER            PIC X(9)   VALUE 'CUST NAME'.
016700     05  FILLER            PIC X(8)   VALUE SPACES.
016800     05  FILLER            PIC X(7)   VALUE 'INVOICE'.
016900     05  FILLER            PIC X(8)   VALUE SPACES.
017000     05  FILLER            PIC X(7)   VALUE 'CURRENT'.
017100     05  FILLER            PIC X(7)   VALUE SPACES.
017200     05  FILLER            PIC X(7)   VALUE 'OVER 30'.
017300     05  FILLER            PIC X(7)   VALUE SPACES.
017400     05  FILLER            PIC X(7)   VALUE 'OVER 60'.
017500     05  FILLER            PIC X(7)   VALUE SPACES.
017600     05  FILLER            PIC X(7)   VALUE 'OVER 90'.
017700     05  FILLER            PIC X(9)   VALUE SPACES.
017800     05  FILLER            PIC X(5)   VALUE 'TOTAL'.
017900     05  FILLER            PIC X(25)  VALUE SPACES.
018000
018100
018200 01  DL-DETAIL.
018300     05  FILLER            PIC X(5)   VALUE SPACES.
018400     05  CUST-NO-DL        PIC X(2).
018500     05  FILLER            PIC X(6)   VALUE SPACES.
018600     05  CUST-NAME-DL      PIC X(15).
018700     05  FILLER            PIC X(3)   VALUE SPACES.
018800     05  CUST-INV-DL       PIC X(5).
018900     05  FILLER            PIC X(4)   VALUE SPACES.
019000     05  CURRENT-DL        PIC Z,ZZZ,ZZ9.99-.
019100     05  FILLER            PIC X(1)   VALUE SPACES.
019200     05  OVER-30-DL        PIC Z,ZZZ,ZZ9.99-.
019300     05  FILLER            PIC X(1)   VALUE SPACES.
019400     05  OVER-60-DL        PIC Z,ZZZ,ZZ9.99-.
019500     05  FILLER            PIC X(1)   VALUE SPACES.
019600     05  OVER-90-DL        PIC Z,ZZZ,ZZ9.99-.
019700     05  FILLER            PIC X(38)  VALUE SPACES.
019800
019900
020000 01  TL-CUST-TOTAL.
020100     05  FILLER            PIC X(5)   VALUE SPACES.
020200     05  FILLER            PIC X(19)
020300                  VALUE 'TOTALS FOR CUST NO '.
020400     05  CUST-NO-TL        PIC X(2).
020500     05  FILLER            PIC X(14)  VALUE SPACES.
020600     05  CURRENT-TL        PIC Z,ZZZ,ZZ9.99-.
020700     05  FILLER            PIC X(1)   VALUE SPACES.
020800     05  OVER-30-TL        PIC Z,ZZZ,ZZ9.99-.
020900     05  FILLER            PIC X(1)   VALUE SPACES.
021000     05  OVER-60-TL        PIC Z,ZZZ,ZZ9.99-.
021100     05  FILLER            PIC X(1)   VALUE SPACES.
021200     05  OVER-90-TL        PIC Z,ZZZ,ZZ9.99-.
021300     05  FILLER            PIC X(1)   VALUE SPACES.
021400     05  CUST-TOTAL-TL     PIC Z,ZZZ,ZZ9.99-.
021500     05  FILLER            PIC X(24)  VALUE SPACES.
021600
021700
021800 01  GTL-REPORT-TOTALS.
021900     05  FILLER            PIC X(5)   VALUE SPACES.
022000     05  FILLER            PIC X(21)
022100                  VALUE 'GRAND TOTALS         '.
022200     05  FILLER            PIC X(14)  VALUE SPACES.
022300     05  CURRENT-GTL       PIC Z,ZZZ,ZZ9.99-.
022400     05  FILLER            PIC X(1)   VALUE SPACES.
022500     05  OVER-30-GTL       PIC Z,ZZZ,ZZ9.99-.
022600     05  FILLER            PIC X(1)   VALUE SPACES.
022700     05  OVER-60-GTL       PIC Z,ZZZ,ZZ9.99-.
022800     05  FILLER            PIC X(1)   VALUE SPACES.
022900     05  OVER-90-GTL       PIC Z,ZZZ,ZZ9.99-.
023000     05  FILLER            PIC X(38)   VALUE SPACES.
023100
023200
023300 01  ERROR-MESSAGE-EL     PIC X(133).
023400
023500 01  ERROR-RECORD-EL.
023600     05  CUST-NO-EL       PIC 99.
023700     05  FILLER           PIC X(4)   VALUE SPACES.
023800     05  INV-NO-EL        PIC 9(5).
023900     05  FILLER           PIC X(4)   VALUE SPACES.
024000     05  AGED-CODE-EL     PIC 9.
024100     05  FILLER           PIC X(4)   VALUE SPACES.
024200     05  INV-AMT-EL       PIC Z,ZZZ,ZZ9.99-.
024300     05  FILLER           PIC X(4)   VALUE SPACES.
024400     05  PAID-DATE-EL     PIC 9(6).
024500     05  FILLER           PIC X(57).
024600
024700 01  DISPLAY-LINE.
024800     05  DISP-MESSAGE     PIC X(452).
024900     05  DISP-VALUE       PIC ZZZ99.
025000
025100
025200 PROCEDURE DIVISION.
025300
025400 000-MAINLINE SECTION.
025500
025600     OPEN INPUT  MI-INPUT-FILE
025700          OUTPUT MI-OUTPUT-SRT-FILE
025800          OUTPUT MATB-OUTPUT-RPT.
025900     SORT SW-MI-SORT-FILE
026000          ON ASCENDING KEY CUST-NAME-SRT-WK
026100                           INV-NO-SRT-WK
026200          INPUT  PROCEDURE 200-SRT-INPUT-PROCD THRU 200-EXIT
026300          OUTPUT PROCEDURE 300-SRT-OUTPUT-PROCD THRU 300-EXIT.
026400     PERFORM 550-DISPLAY-PROG-DIAG THRU 550-DISPLAY-PROG-DIAG-EXIT
026500     CLOSE MI-INPUT-FILE
026600           MI-OUTPUT-SRT-FILE
026700           MATB-OUTPUT-RPT.
026800     MOVE ZERO TO RETURN-CODE.
026900     GOBACK.
027000
027100
027200 200-SRT-INPUT-PROCD SECTION.
027300
027400     MOVE 'NO ' TO WS-EOF-INPUT-SW.
027500     PERFORM 800-READ-INPUT-FILE THRU 800-READ-INPUT-FILE-EXIT.
027600     IF EOF-INPUT
027700        DISPLAY 'ERROR!! dd INPUT FILE EMPTY!'
027800        GO TO 200-EXIT.
027900     PERFORM 210-PRSS-INPUT-RECORDS THRU
028000                         210-PRSS-INPUT-RECORDS-EXIT
028100         UNTIL EOF-INPUT.
028200
028300 200-EXIT.
028400     EXIT.
028500 210-PRSS-INPUT-RECORDS.
028600
028700     PERFORM 214-CK-INPUT-DATA THRU 214-CK-INPUT-DATA-EXIT.
028800     IF INPUT-OK AND UNPAID
028900        PERFORM 218-SEARCH-FOR-CUST-NAME THRU
029000                     218-SEARCH-FOR-CUST-NAME-EXIT
029100        IF NAME-FOUND
029200           PERFORM 850-FORMAT-RELEASE THRU 850-FORMAT-RELEASE-EXIT
029300        ELSE
029400           NEXT SENTENCE
029500     ELSE
029600        NEXT SENTENCE.
029700     PERFORM 800-READ-INPUT-FILE THRU 800-READ-INPUT-FILE-EXIT.
029800
029900
030000 210-PRSS-INPUT-RECORDS-EXIT.
030100     EXIT.
030200
030300
030400 214-CK-INPUT-DATA.
030500
030600     MOVE 'YES' TO WS-INPUT-OK-SW.
030700     IF PAID-DATE-IN-NUM IS NUMERIC
030800
030900        IF PAID-DATE-IN-NUM IS ZERO
031000           ADD 1 TO WS-UNPD-CTR
031100           MOVE 'NO ' TO WS-PAID-SW
031200
031300           IF AGED-CODE-IN IS > ZERO AND < 15
031400
031500              IF CUST-NO-IN IS NUMERIC
031600
031700                 IF INV-NO-IN-NUM IS NUMERIC
031800
031900                    IF INV-AMT-IN IS NUMERIC
032000                       MOVE 'YES' TO WS-INPUT-OK-SW
032100                    ELSE
032200                        MOVE
032300                        '** ERROR **  INVOICE AMOUNT NOT NUMERIC '
032400                               TO ERROR-MESSAGE-EL
032500                        PERFORM 700-ERROR-DISPLAY THRU
032600                                           700-ERROR-DISPLAY-EXIT
032700
032800                 ELSE
032900                     MOVE
033000                     '** ERROR **  INVOICE NUMBER NOT NUMERIC '
033100                            TO ERROR-MESSAGE-EL
033200                     PERFORM 700-ERROR-DISPLAY THRU
033300                                        700-ERROR-DISPLAY-EXIT
033400
033500              ELSE
033600                  MOVE
033700                  '** ERROR **  CUSTOMER NUMBER NOT NUMERIC '
033800                         TO ERROR-MESSAGE-EL
033900                  PERFORM 700-ERROR-DISPLAY THRU
034000                                      700-ERROR-DISPLAY-EXIT
034100
034200           ELSE
034300               MOVE
034400               '** ERROR **  INVALID AGED CODE '
034500                      TO ERROR-MESSAGE-EL
034600               PERFORM 700-ERROR-DISPLAY THRU
034700                                   700-ERROR-DISPLAY-EXIT
034800
034900        ELSE
035000            ADD 1 TO WS-PAID-CTR
035100            MOVE 'YES' TO WS-PAID-SW
035200
035300     ELSE
035400         MOVE
035500         '** ERROR **  PAID DATE NOT NUMERIC'
035600                TO ERROR-MESSAGE-EL
035700         PERFORM 700-ERROR-DISPLAY THRU
035800                             700-ERROR-DISPLAY-EXIT.
035900
036000
036100
036200 214-CK-INPUT-DATA-EXIT.
036300     EXIT.
036400
036500
036600
036700 218-SEARCH-FOR-CUST-NAME.
036800
036900     MOVE SPACES TO WS-NAME-FOUND-SW.
037000     SET CUST-INDEX TO 12.
037100     SEARCH CUSTOMER-REC
037200         AT END
037300             MOVE 'NO ' TO WS-NAME-FOUND-SW
037400             MOVE
037500             '** ERROR **  CUSTOMER NAME NOT FOUND'
037600                TO ERROR-MESSAGE-EL
037700             PERFORM 700-ERROR-DISPLAY THRU
037800                                 700-ERROR-DISPLAY-EXIT
037900         WHEN
038000             CUSTOMER-CODE (CUST-INDEX) IS EQUAL TO CUST-NO-IN
038100                MOVE 'YES' TO WS-NAME-FOUND-SW
038200                MOVE CUSTOMER-NAME (CUST-INDEX) TO
038300                           CUST-NAME-SRT-WK.
038400
038500
038600 218-SEARCH-FOR-CUST-NAME-EXIT.
038700     EXIT.
038800
038900
039000
039100 300-SRT-OUTPUT-PROCD.
039200
039300     PERFORM 320-INITIALIZE-OUTPUT THRU
039400                  320-INITIALIZE-OUTPUT-EXIT.
039500     PERFORM 900-RETURN-SRTD-REC THRU 900-RETURN-SRTD-REC-EXIT.
039600     IF EOF-SRT-OUTPUT
039700        DISPLAY 'SORTED RECORDS NOT AVAILIBLE'
039800        GO TO 300-EXIT.
039900     MOVE CUST-NO-SRT-WK TO WS-PREVIOUS-CUST-NO.
040000     MOVE  CUST-NAME-SRT-WK TO CUST-NAME-DL.
040100     PERFORM 340-PRSS-SORTED-OUTPUT THRU
040200                                    340-PRSS-SORTED-OUTPUT-EXIT
040300        UNTIL  EOF-SRT-OUTPUT.
040400     PERFORM 400-PRSS-CUST-BREAK THRU 400-PRSS-CUST-BREAK-EXIT.
040500     PERFORM 500-PRSS-GRAND-TOTALS THRU
040600                                    500-PRSS-GRAND-TOTALS-EXIT.
040700
040800
040900 300-EXIT.
041000     EXIT.
041100
041200
041300
041400 320-INITIALIZE-OUTPUT.
041500
041600     MOVE ZEROS TO WS-PAGE-COUNT,
041700                   WS-LINE-SPACING,
041800                   WS-CURRENT-TL,
041900                   WS-OVER-30-TL,
042000                   WS-OVER-60-TL,
042100                   WS-OVER-90-TL,
042200                   WS-CUST-TOTAL-TL,
042300                   WS-CURRENT-GTL,
042400                   WS-OVER-30-GTL,
042500                   WS-OVER-60-GTL,
042600                   WS-OVER-90-GTL.
042700      MOVE 'NO ' TO WS-EOF-SRT-OUTPUT-SW.
042800      MOVE SPACES TO DL-DETAIL.
042900      MOVE '01-01-92' TO RPT-DATE.
043000
043100
043200 320-INITIALIZE-OUTPUT-EXIT.
043300     EXIT.
043400
043500
043600 340-PRSS-SORTED-OUTPUT.
043700
043800     IF CUST-NO-SRT-WK IS NOT EQUAL TO WS-PREVIOUS-CUST-NO
043900        PERFORM 400-PRSS-CUST-BREAK THRU
044000                                    400-PRSS-CUST-BREAK-EXIT.
044100     PERFORM 343-DETAIL-LINE-PRSS THRU
044200                                    343-DETAIL-LINE-PRSS-EXIT.
044300     PERFORM 950-WRITE-OUTPUT-REC THRU
044400                                    950-WRITE-OUTPUT-REC-EXIT.
044500     PERFORM 900-RETURN-SRTD-REC THRU
044600                                    900-RETURN-SRTD-REC-EXIT.
044700
044800
044900
045000 340-PRSS-SORTED-OUTPUT-EXIT.
045100     EXIT.
045200
045300
045400 343-DETAIL-LINE-PRSS.
045500
045600     IF WS-LINES-USED IS GREATER THAN WS-LINES-PER-PAGE OR
045700        WS-LINES-USED IS EQUAL TO WS-LINES-PER-PAGE
045800           PERFORM 955-HEADINGS THRU 955-HEADINGS-EXIT.
045900     MOVE  CUST-NO-SRT-WK  TO  CUST-NO-DL.
046000     MOVE  INV-NO-SRT-WK  TO  CUST-INV-DL.
046100     IF AGED-CODE-SRT-WK IS EQUAL TO 1
046200        MOVE  INV-AMT-SRT-WK TO CURRENT-DL
046300        ADD INV-AMT-SRT-WK TO WS-CURRENT-TL
046400     ELSE
046500        IF AGED-CODE-SRT-WK IS EQUAL TO 2
046600           MOVE  INV-AMT-SRT-WK TO OVER-30-DL
046700           ADD INV-AMT-SRT-WK TO WS-OVER-30-TL
046800        ELSE
046900           IF AGED-CODE-SRT-WK IS EQUAL TO 3
047000              MOVE  INV-AMT-SRT-WK TO OVER-60-DL
047100              ADD INV-AMT-SRT-WK TO WS-OVER-60-TL
047200           ELSE
047300              MOVE  INV-AMT-SRT-WK TO OVER-90-DL
047400              ADD INV-AMT-SRT-WK TO WS-OVER-90-TL.
047500
047600     WRITE MATB-OUTPUT-REP-LINE FROM DL-DETAIL
047700         AFTER ADVANCING WS-LINE-SPACING.
047800     ADD WS-LINE-SPACING TO WS-LINES-USED.
047900     MOVE 1 TO WS-LINE-SPACING.
048000     MOVE  SPACES TO DL-DETAIL.
048100
048200
048300 343-DETAIL-LINE-PRSS-EXIT.
048400     EXIT.
048500
048600
048700 400-PRSS-CUST-BREAK.
048800
048900     COMPUTE WS-CUST-TOTAL-TL = WS-CURRENT-TL +
049000                                WS-OVER-30-TL +
049100                                WS-OVER-60-TL +
049200                                WS-OVER-90-TL .
049300     MOVE 2 TO WS-LINE-SPACING.
049400     MOVE WS-PREVIOUS-CUST-NO TO CUST-NO-TL.
049500     MOVE WS-CURRENT-TL TO CURRENT-TL.
049600     MOVE WS-OVER-30-TL TO OVER-30-TL.
049700     MOVE WS-OVER-60-TL TO OVER-60-TL.
049800     MOVE WS-OVER-90-TL TO OVER-90-TL.
049900     MOVE WS-CUST-TOTAL-TL TO CUST-TOTAL-TL.
050000     WRITE MATB-OUTPUT-REP-LINE FROM TL-CUST-TOTAL
050100         AFTER ADVANCING WS-LINE-SPACING.
050200     ADD WS-LINE-SPACING TO WS-LINES-USED.
050300     ADD WS-CURRENT-TL TO WS-CURRENT-GTL.
050400     ADD WS-OVER-30-TL TO WS-OVER-30-GTL.
050500     ADD WS-OVER-60-TL TO WS-OVER-60-GTL.
050600     ADD WS-OVER-90-TL TO WS-OVER-90-GTL.
050700     IF NOT EOF-SRT-OUTPUT
050800        MOVE ZEROS TO WS-CURRENT-TL,
050900                      WS-OVER-30-TL,
051000                      WS-OVER-60-TL,
051100                      WS-OVER-90-TL,
051200                      WS-CUST-TOTAL-TL
051300        MOVE CUST-NO-SRT-WK TO WS-PREVIOUS-CUST-NO
051400        MOVE CUST-NAME-SRT-WK TO CUST-NAME-DL
051500        IF WS-LINES-USED IS GREATER THAN WS-LINES-PER-PAGE OR
051600           WS-LINES-USED IS EQUAL TO WS-LINES-PER-PAGE
051700           PERFORM 955-HEADINGS THRU 955-HEADINGS-EXIT
051800        ELSE
051900           MOVE 2 TO WS-LINE-SPACING.
052000
052100
052200 400-PRSS-CUST-BREAK-EXIT.
052300     EXIT.
052400
052500
052600 500-PRSS-GRAND-TOTALS.
052700
052800     MOVE 2 TO WS-LINE-SPACING.
052900     MOVE  WS-CURRENT-GTL TO CURRENT-GTL.
053000     MOVE  WS-OVER-30-GTL TO OVER-30-GTL.
053100     MOVE  WS-OVER-60-GTL TO OVER-60-GTL.
053200     MOVE  WS-OVER-90-GTL TO OVER-90-GTL.
053300     WRITE MATB-OUTPUT-REP-LINE FROM GTL-REPORT-TOTALS
053400         AFTER ADVANCING WS-LINE-SPACING.
053500
053600
053700 500-PRSS-GRAND-TOTALS-EXIT.
053800     EXIT.
053900
054000
054100
054200
054300 550-DISPLAY-PROG-DIAG.
054400
054500     DISPLAY '****     B999BLK2 RUNNING    ****'.
054600     DISPLAY '                                                 '.
054700     MOVE 'MONTHLY INVOICE RECORDS READ                 '  TO
054800          DISP-MESSAGE.
054900     MOVE WS-READ-CTR TO DISP-VALUE.
055000     DISPLAY DISPLAY-LINE.
055100     DISPLAY '                                                 '.
055200     MOVE 'MONTHLY INVOICE RECORDS RELEASED TO SORT     '  TO
055300          DISP-MESSAGE.
055400     MOVE WS-REL-CTR TO DISP-VALUE.
055500     DISPLAY DISPLAY-LINE.
055600     MOVE 'MONTHLY INVOICE RECORDS RETURNED FROM SORT   '  TO
055700          DISP-MESSAGE.
055800     MOVE WS-RETR-CTR TO DISP-VALUE.
055900     DISPLAY DISPLAY-LINE.
056000     DISPLAY '                                                 '.
056100     MOVE 'MONTHLY INVOICE RECORDS WRITTEN TO MRA       '  TO
056200          DISP-MESSAGE.
056300     MOVE WS-WRTN-CTR TO DISP-VALUE.
056400     DISPLAY DISPLAY-LINE.
056500     DISPLAY '                                                 '.
056600     MOVE 'MONTHLY INVOICE PAID                         '  TO
056700          DISP-MESSAGE.
056800     MOVE WS-PAID-CTR TO DISP-VALUE.
056900     DISPLAY DISPLAY-LINE.
057000     MOVE 'MONTHLY INVOICE UNPAID                       '  TO
057100          DISP-MESSAGE.
057200     MOVE WS-UNPD-CTR TO DISP-VALUE.
057300     DISPLAY DISPLAY-LINE.
057400     DISPLAY '                                                 '.
057500     DISPLAY '****     B999BLK2 EOJ        ****'.
057600
057700
057800
057900 550-DISPLAY-PROG-DIAG-EXIT.
058000     EXIT.
058100
058200
058300 700-ERROR-DISPLAY.
058400
058500     MOVE 'NO ' TO WS-INPUT-OK-SW.
058600     DISPLAY ERROR-MESSAGE-EL.
058700     MOVE CUST-NO-IN TO CUST-NO-EL.
058800     MOVE INV-NO-IN-NUM TO INV-NO-EL.
058900     MOVE AGED-CODE-IN TO AGED-CODE-EL.
059000     MOVE INV-AMT-IN TO INV-AMT-EL.
059100     MOVE PAID-DATE-IN-NUM TO PAID-DATE-EL.
059200     DISPLAY ERROR-RECORD-EL.
059300
059400 700-ERROR-DISPLAY-EXIT.
059500     EXIT.
059600
059700
059800 800-READ-INPUT-FILE.
059900
060000     READ MI-INPUT-FILE INTO MI-INPUT-REC
060100         AT END  MOVE 'YES' TO WS-EOF-INPUT-SW,
060200                 GO TO 800-READ-INPUT-FILE-EXIT.
060300     ADD 1 TO WS-READ-CTR.
060400
060500 800-READ-INPUT-FILE-EXIT.
060600     EXIT.
060700
060800
060900 850-FORMAT-RELEASE.
061000
061100     MOVE CUST-NO-IN TO CUST-NO-SRT-WK.
061200     MOVE INV-NO-IN TO INV-NO-SRT-WK.
061300     MOVE AGED-CODE-IN TO AGED-CODE-SRT-WK.
061400     MOVE INV-AMT-IN TO INV-AMT-SRT-WK.
061500     MOVE PAID-DATE-IN TO  PAID-DATE-SRT-WK.
061600     RELEASE SW-MI-SORT-WORK.
061700     ADD 1 TO WS-REL-CTR.
061800
061900
062000 850-FORMAT-RELEASE-EXIT.
062100     EXIT.
062200
062300
062400
062500
062600 900-RETURN-SRTD-REC.
062700
062800     RETURN SW-MI-SORT-FILE
062900         AT END  MOVE 'YES' TO WS-EOF-SRT-OUTPUT-SW,
063000                 GO TO 900-RETURN-SRTD-REC-EXIT.
063100     ADD 1 TO WS-RETR-CTR.
063200
063300 900-RETURN-SRTD-REC-EXIT.
063400     EXIT.
063500
063600
063700 950-WRITE-OUTPUT-REC.
063800
063900     WRITE MI-OUTPUT-SRT-RECORD FROM SW-MI-SORT-WORK.
064000     ADD 1 TO WS-WRTN-CTR.
064100
064200 950-WRITE-OUTPUT-REC-EXIT.
064300     EXIT.
064400
064500
064600 955-HEADINGS.
064700
064800     ADD 1 TO WS-PAGE-COUNT.
064900     MOVE WS-PAGE-COUNT TO RPT-PAGE-NO.
065000     WRITE MATB-OUTPUT-REP-LINE FROM HL-HEADER-1
065100         AFTER ADVANCING PAGE.
065200     MOVE 1 TO WS-LINES-USED.
065300     MOVE 2 TO WS-LINE-SPACING.
065400     WRITE MATB-OUTPUT-REP-LINE FROM HL-HEADER-2
065500         AFTER ADVANCING WS-LINE-SPACING.
065600     ADD WS-LINE-SPACING TO WS-LINES-USED.
065700
065800
065900 955-HEADINGS-EXIT.
066000     EXIT.
